plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1' // Плагін для shading
}

group = 'org.degree'
version = '1.0.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "mojang"
        url = "https://libraries.minecraft.net/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven { url 'https://repo.luckperms.net/' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
    implementation 'org.bstats:bstats-bukkit:3.0.0'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    // JDBI Core
    implementation 'org.jdbi:jdbi3-core:3.38.0'

    // SQLite JDBC драйвер
    implementation 'org.xerial:sqlite-jdbc:3.45.1.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // Логування для JDBI (опціонально, для налагодження)


}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

// ShadowJar конфігурація
shadowJar {
    archiveBaseName.set('BIRTHDAY') // Назва файлу
    archiveVersion.set('1.0.0-DEV') // Версія файлу
    archiveClassifier.set('') // Без класифікатора
    mergeServiceFiles()
    minimize()
    exclude 'org/sqlite/native/**'

    // Переносим все зависимости в пространство имен org.degree.libs
    relocate 'org.jdbi', 'org.degree.libs.jdbi'
    relocate 'org.sqlite', 'org.degree.libs.sqlite'
    relocate 'org.bstats', 'org.degree.libs.bstats'
}


build.dependsOn shadowJar
